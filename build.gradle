buildscript {
    ext.kotlinVersion = '1.1.2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "org.jetbrains.kotlin.jvm" version "1.1.2"
}

group 'io.github.neyb.shoulk'
version '0.1.0'

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

junitPlatform {
    engines {
        include 'spek'
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }

            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/neyb/shoulk.git'
        version {
            name = project.version
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    testCompile 'org.jetbrains.spek:spek-api:1.0.89'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.0.89'
    testRuntime 'org.junit.platform:junit-platform-console:1.0.0-M2'
}