buildscript {
    ext.kotlinVersion = '1.1.2-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven-publish'

group 'io.github.neyb.shoulk'
version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"

    testCompile 'org.jetbrains.spek:spek-api:1.0.89'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.0.89'
    testRuntime 'org.junit.platform:junit-platform-console:1.0.0-M2'
}

junitPlatform {
    engines {
        include 'spek'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUser?:"bintray user"
    key = bintrayKey?:"bintray api key"
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/neyb/shoulk.git'
        version {
            name = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}